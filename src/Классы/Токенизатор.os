
Перем _Строка;
Перем _Курсор;
Перем _Спецификация;

Процедура ПриСозданииОбъекта(Спецификация)

	_Спецификация = Спецификация;
	
КонецПроцедуры

Процедура Инит(Строка) Экспорт

	_Строка = Строка;
	_Курсор = 1;
	
КонецПроцедуры

Функция ЕстьЕщеТокены() Экспорт
	Возврат _Курсор <= СтрДлина(_Строка);
КонецФункции

Функция СледующийТокен() Экспорт

	Если Не ЕстьЕщеТокены() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Строка = Сред(_Строка, _Курсор);

	Для Каждого СпецификацияТокена Из _Спецификация Цикл

		ЗначениеТокена = СпецификацияТокена.Проверить(Строка);

		Если ЗначениеТокена = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		НачалоТокена = _Курсор;

		_Курсор = НачалоТокена + ЗначениеТокена.Размер;

		Если СпецификацияТокена.ТипТокена() = Неопределено Тогда
			Возврат СледующийТокен();
		КонецЕсли;

		Возврат Новый Токен(ЗначениеТокена.Значение, СпецификацияТокена.ТипТокена(), НачалоТокена, ЗначениеТокена.Размер); 

	КонецЦикла;

	ВызватьИсключение СтрШаблон("Не найдено токенов в строке %1", Строка);

	// Возврат Неопределено;
КонецФункции